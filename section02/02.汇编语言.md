# 简介

​	项目在Mac10.14系统下开发，采用Intel格式的汇编语法，在基于8086架构CPU的虚拟环境上调试。这个文档简单介绍开发所需的工具、汇编语法。



# 相关工具

​	基础的开发工具包括：汇编器、链接器、调试器。常见的汇编器有as、masm、nasm等。汇编语言有Intel格式、AT&T格式两种语法格式。

​	项目采用nasm作为汇编器，这个汇编器使用Intel格式语法，Mac下安装命令：

```
	brew install nasm
```

​	程序在虚拟环境下调试，可使用DOSBox(https://www.dosbox.com/)、debug.exe搭建调试环境。方法如下：

```
	1）安装DOSBox
	2）创建~/Documents/workspace/MyDOSBox（任意）目录
	3）拷贝debug.exe到该目录下
	4）打开DOSBox，运行命令
		mount c ~/Documents/workspace/MyDOSBox
		c:
	5）配置启动自动运行命令
		打开~/Library/Preferences/DOSBox 0.74-3 Preferences文件
		在[autoexec]处，即文件末尾添加命令
```



# DEBUG工具命令

​	命令模式下，地址均为16位数，省略0x前缀。

```
	1)显示寄存器:
		r
	2)显示字节数据:
		d
		d 偏移地址
		d 段地址:偏移地址
		d 段地址:偏移地址1 偏移地址2
	3)显示汇编指令:
		u
		u 偏移地址
		u 段地址:偏移地址
		u 段地址:偏移地址1 偏移地址2
	4)写入汇编指令:
		a
		a 偏移地址
		a 段地址:偏移地址
	5)修改字节数据:
		e 偏移地址
		e 段地址:偏移地址
	6)运行一条指令:
		t
	7)运行一段指令:
		p
	8)退出:
		q
```



# 第一个程序

​	新建文件label.s，写入下面代码：

```
label:
	mov ax, 0x1001
	mov bx, 0x1002
	mov cx, 0x1003
	mov dx, 0x1004
	jmp label
```

​	打开终端，运行nasm label.s命令。

​	打开DOSBox软件，运行debug.exe label命令。

​	在debug命令模式下，输入r查看寄存器，然后每输入t执行一条指令。



# 语法

​	采用Intel格式语法，基于8086架构CPU，16位实模式。

​	汇编程序由汇编指令和伪指令组成，汇编指令最终会翻译成由0和1组成的机器代码，伪指令用于对汇编过程进行控制的指令，没有对应的机器代码。

​	编译下面一段代码：

```
	db 0xb8, 0x01, 0x10, 0xbb, 0x02, 0x10, 0xb9, 0x03
	db 0x10, 0xba, 0x04, 0x10, 0xeb, 0xf2 
```

​	与“第一个程序”编译生成的label进行md5校验，会发现两个文件是一样的。

​	汇编指令在上段代码有对应的字节数据，而伪指令没有。



## 1.通用寄存器

​	通用寄存器一般用来存放数据，相当于变量。

​	CPU有4个通用寄存器：ax、bx、cx、dx，每个寄存器16位，并可分为独立的高、低字节两个8位寄存器。两种寄存器关系：

```
	ax = ah << 8 + al
	bx = bh << 8 + bl
	cx = ch << 8 + cl
	dx = dh << 8 + dl
```



## 2.赋值指令（mov）

​	赋值指令符号为mov，目标变量在左，原数值在右。

```
	mov ax, 0xaa55		# ax = 0xaa55
	mov bl, 0x12			# bl = 0x12
	mov bh, 0x34			# bh = 0x34, 两条指令运行后，bx = 0x3412
```



## 3.寻址方式

​	计算机许多部件，包括键盘、鼠标、内存条、显卡等，都存储数据的地方，统称内存。数据在内存中有序排列，每个数据单元都有一个地址。

​	CPU按段地址和偏移地址组成的地址查找数据，段地址、偏移地址按以下方式组成地址：

```
	地址 = 段地址 << 4 + 偏移地址
```

​	段地址寄存器：ds、es、ss、cs

​	偏移地址寄存器：si、di、sp、ip、dp、dx



## 4.CS、IP寄存器（指令相关）

​	CPU从cs:ip组合地址中读取指令，然后ip加上所读指令字节数，分析、执行指令，重复这个过程。即cs:ip指向将要执行的指令。

​	无法直接修改，只能通过转移指令实现跳转。



## 5.转移指令1（jmp、loop、jcxz）

​	转移指令通过改变cs:ip的值，实现指令跳转。

​	1）转移指令jmp用法：

```
	jmp 0x2000:0x100		# 跳转到 (0x2000 << 4 + 0x100) = 0x20100
	jmp ax		# 跳转到 (cs << 4 + ax) 的地址
label:
	mov ax, 0x10
	jmp label		# 跳转到label处，编译器提供支持，将label翻译为相应数字
```

​	2）转移指令loop用法：

​	执行loop指令后，cx减1，如果cx为0，退出循环。

```
	mov cx, 10
func:
	mov ax, 0x100
	loop func
```

​	3）转移指令jcxz用法：

​	条件转移指令，当cx等于0时跳转。用法与jmp类似。

```
label:
	jcxz label
```



## 6.DS、ES、BX、SI、DI寄存器（数据相关）

​	提供内存寻址的段地址、偏移地址。

​	一般用法，ds指定数据来源段地址，es指定数据输出段地址。

​	无法直接修改，只能通过其它寄存器赋值：

```
	mov ax, cs
	mov ds, ax
	mov es, ax
```

​	1）直接寻址，段地址由ds给出，偏移地址由中括号[ ]给出：

```
	mov ax, [ds:0x0100]
```

​	2）基址寻址，ds、es的基址寄存器是bx，可以带一个立即数偏移量：

```
	mov ax, [ds:bx]
	mov ax, [ds:(bx + 3)]
	mov [es:bx], ax
	mov [es:(bx + 3)], ax
```

​	3）变址寻址，ds、es的变址寄存器是si、di，[ ]中只能包含si和di的其中一个，可以带一个立即数偏移量：

```	
	mov ax, [ds:si]
	mov ax, [ds:(si + 3)]
	mov [es:di], ax
	mov [es:(di + 3)], ax
```

​	4）基址变址寻址：

```
	mov ax, [ds:(bx + si + 3)]
	mov [es:(bx + di + 3)], ax
```

​	以上段地址寄存器可省略，段地址由ds给出：

```
	mov ax, [0x0100]
	mov ax, [bx + 3]
	mov [bx + di + 3], ax
```



## 7.定义数据指令（db、dw、dd）

​	db定义字节类型变量，占1个字节单元(8bit)。dw定义字类型变量，占2个字节单元(16bit)。dd定义双字类型变量，占4个字节单元(32bit)。dq定义四字类型变量，占8个字节单元(64bit)。

```
	db 0x12, 64, 0xaa, 0x55
	db "Hello!"
	dw 0xaa55
	dd 0x12345678
```



## 8.示例程序1：复制数据

​	下面程序实现了，将data1处的字节数据复制到data2处：

```
org 0x0100

	jmp start

data1:
	db 1, 2, 3, 4, 5, 6, 7, 8

data2:
	db 0, 0, 0, 0, 0, 0, 0, 0

start:
	mov si, data1
	mov di, data2

	mov ax, 0
	mov cx, 8

copy:
	mov al, [ds:si]
	mov [es:di], al
	inc si		;si自加1
	inc di		;di自加1
	loop copy
```

### 伪指令"org 0x0100"说明：

```
  ; 1）一条伪指令，告诉编译器程序装载的偏移地址。
  ; 2）debug调试程序开始，输入u、r查看，发现指令装载在0x73f:0x0100处，IP=0x0100，
  ; 3）如果不使用"org 0x0100"指令，编译器在翻译标签时，标号从0开始算的。
  ;	"mov si, data1"指令保存的标号，与data1数据在内存中的偏移地址不一样，
  ;	导致"mov al, [ds:si]"指令找到的数据不是data1段的。
  ; 4）"loop copy"指令不会受到影响。因为转移指令翻译成机器码时，使用的是指令间的偏移量。
```



## 9.示例程序2：复制数据改进

​	下面程序对“示例程序1”风格做了改进：

```
org 0x0100

[section .data]

	_data1 db "Hello, world!", 0x0a		;0x0a对应换行符
	_len equ ($ - _data1)
	_data2 times (_len) db 0

[section .text]
  
	mov si, _data1
	mov di, _data2
    
	mov bx, 0
	mov cx, _len

_copy:
	mov al, [ds:si]
	mov [es:di], al
	inc si		;si自加1
	inc di		;di自加1
	loop _copy
```

​	编译器编译时，将[section .data]代码段自动移动到了[section .text]代码段后面，省略了开头的转移指令。

### 伪指令"_len equ ($ - _data1)"说明：

​	一条伪指令，宏定义 _len 量，等于当前位置到 _data1 位置的差。

### 伪指令"times (_len)"说明：

​	一条伪指令，表示重复执行后续指令 _len 次。



## 10.加法、减法指令（add、sub）

​	1）加法指令add，两者相加，结果存放在左边：

```
	add ax, 1
	add ah, 0xf0
	add ax, [ds:0x100]
	add [ds:0x102], ax
```

​	2）减法指令sub，左边减去右边，结果存放于左边：

```
	sub ax, 1
	sub ah, 0xf0
	sub ax, [ds:0x100]
	sub [ds:0x102], ax
```

​	3）自加一指令inc：

```
	inc cx
```

​	4）自减一指令dec：

```
	dec cx
```



## 11.除法指令（div）

​	1）除法指令div，除数8bit的情况

​	**被除数为16bit，存放在ax中。计算结果，al存放商，ah存放余数**：

```
	mov ax, 93
	mov bl, 13
	div bl		;计算 93 / 13 = 7 ... 2
	div byte [ds:0x100]		;内存单元作除数
```

​	2）除法指令div，除数16bit的情况

​	**被除数为32bit，ax存放低16bit，dx存放高16bit。计算结果，ax存放商，dx存放余数**：

```
	mov ax, 0x1234
	mov dx, 0x0010
	mov bx, 0x100
	div bx		;计算 0x101234 / 0x100 = 0x1012 ... 0x34
	div word [ds:0x100]		;内存单元作除数
```



## 12.乘法指令（mul）

​	与除法类似，分两种情况。

​	1）乘法指令mul，乘数8bit的情况

​	**另一个乘数存放在al中。计算结果，存放在ax中**：

```
	mov al, 7
	mov bl, 13
	mul bl		;计算 7 * 13 = 91
	mul byte [ds:0x100]		;内存单元作乘数
```

​	2）乘法指令mul，乘数16bit的情况

​	**另一个乘数存放在ax中。计算结果，ax存放低16bit，dx存放高16bit**：

```
	mov ax, 0x1234
	mov bx, 0x100
	mul bx		;计算 0x1234 * 0x100 = 0x123400
	mul word [ds:0x100]		;内存单元作乘数
```



## 13.SS、SP寄存器（栈相关）

​	提供栈的段地址、偏移地址，ss:sp组合地址指向栈顶。

​	一般用法，ss指向栈底，sp指定栈的大小。

​	无法直接修改，只能通过其它寄存器赋值：

```
	mov ax, cs
	mov ss, ax
	mov sp, 128
```



## 14.入栈、出栈指令（push、pop）

​	1）入栈指令push，入栈两个字节，每次操作sp减2：

```
	push ax
	push 0x1234
```

​	2）出栈指令pop，出栈两个字节，每次操作sp加2：

```
	pop bx
```

​	**push/pop指令不会检测边界**，需要开发者谨慎设置。栈的极限大小是2^16字节。

​	栈用于交换数据示例：

```
	mov ax, 0x1234
	mov bx, 0x5678
	push ax
	push bx
	pop ax
	pop bx		;运行结果，交换了ax、bx内容
```



## 15.转移指令2（call、ret、retf）

​	1）call/ret用法：

```
	call func
func:
	mov ax, 0x10
	ret
```

​	执行call指令后，ip入栈。执行ret指令后，ip出栈。

​	2）段间转移，call/retf用法：

```
	call 0x7c00:0x100
```

​	执行call指令后，cs、ip入栈。

```
	retf
```

​	执行ret指令后，ip、cs出栈。



## 16.标志位寄存器

​	16bit寄存器，每个位都有专门的含义。寄存器结构：

| 15 | 14 | 13 | 12 | 11 | 10 | 9 | 8 | 7 | 6 | 5 | 4 | 3 | 2 | 1 | 0 |
| :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: | :--: |
|  |  |  |  | OF | DF | IF | TF | SF | ZF |  | AF |  | PF |  | CF |

​	标志位值为1或0时的调试信息：

| 标志 | OF | DF | SF | ZF | PF | CF |
| :--: | :--: | :--: | :--: | :--: | :--: | :--: |
| 真/值为1 | OV | DN | NG | ZR | PE | CY |
| 假/值为0 | NV | UP | PL | NZ | PO | NC |

### CF标志位

​	Carry Flag，记录**无符号数运算时**结果是否溢出。

​	如加法产生超出范围的进位，减法产生从更高位的借位，都会用这个标志记录。

### ZF标志位

​	Zero Flag，记录**（add、sub、mul、div、inc、or、and指令）**运算结果是否为0。

### PF标志位

​	Parity Flag，记录运算结果二进制位中1的个数是否为偶数。

### SF标志位

​	Sign Flag，记录运算结果是否为负。

### OF标志位

​	Overflow Flag，记录**有符号数运算时**结果是否溢出。

### DF标志位

​	Direction Flag，记录



## 17.溢出位加法、减法指令（adc、sbb）

​	1）adc指令

​	实现加法运算，并加上CF标志位。32位加法运算示例：

```
	mov ax, 0xffff
	mov bx, 0x1000
	add ax, 0xffff
	adc bx, 0			;计算 0x1000ffff + 0xffff = 0x1001fffe
```

​	2）sbb指令

​	实现减法运算，并减去CF标志位。32位减法运算示例：

```
	mov ax, 0xf000
	mov bx, 0x1000
	sub ax, 0xffff
	sbb bx, 0			;计算 0x1000f000 - 0xffff = 0x0ffff001
```



## 18.比较指令、转移指令3（cmp、je、jne等）

​	1）比较指令cmp

​	比较两个数是否相等。示例：

```
	cmp ax, bx
```

​	指令通过减法实现比较，但不保存计算值，通过修改ZF、CF标志位输出结果。

​	当ZF = 1时，相等，当ZF = 0时，不相等。

​	当CF = 1时，小于，当CF = 0时，不小于。

​	2）转移指令3

​	先比较，再执行转移。配合cmp使用，根据ZF、CF标志位情况跳转。

| 指令 |     说明      |     功能     |    标志位条件    |
| :--: | :-----------: | :----------: | :--------------: |
|  je  |   jmp equal   |  等于则转移  |      ZF = 1      |
| jne  | jmp not equal | 不等于则转移 |      ZF = 0      |
|  jb  |   jmp below   |  小于则转移  |      CF = 1      |
| jnb  | jmp not below | 不小于则转移 |      CF = 0      |
|  ja  |   jmp above   |  大于则转移  | CF = 0 且 ZF = 0 |
| jna  | jmp not above | 不大于则转移 | CF = 1 或 ZF = 1 |

​	示例：

```
	cmp ax, bx
label:
	je label		; ax = bx
	jne label		; ax != bx
	jb label		; ax < bx
	jnb label		; ax >= bx
	ja label		; ax > bx
	jna label		; ax <= bx
```



## 19.串传送指令（movsb、movsw、cld等）

​	传送数据方向由DF标志位控制。DF = 0时，每次操作后si、di递增；DF = 1时，每次操作后si、di递减。

​	1）以字节为单位传送

```
	movsb
```

​	功能是将ds:si中的字节送入es:di中，相当于：

```
	mov al, byte [ds:si]
	mov [es:di], al
	inc si			;假设 DF = 0
	inc di
```

​	2）以字为单位传送

```
	movsw
```

​	功能是将ds:si中的字送入es:di中，相当于：

```
	mov ax, word [ds:si]
	mov [es:di], ax
	add si, 2			;假设 DF = 0
	add di, 2
```

​	3）与rep配合使用

```
	rep movsb
	rep movsw
```

​	rep的作用是根据cx的值，重复执行后面的串传送指令，相当于：

```
s1: 
	movsb
	loop s1
s2: 
	movsw
	loop s2
```

​	4）改变传送的方向

​	cld指令，将标志寄存器的DF位置0。std指令，将标志寄存器的DF位置1。

```
	cld			;令DF = 0
	std			;令DF = 1
```

​	

## 20.示例程序3：使用串传送指令复制数据

​	程序示例：

```
org 0x100

[section .data]

_msg:
	db 0x0a, "Hello, world!", 0x0a
_msg_len equ ($ - _msg)

_blank:
	times (_msg_len) db 0

[section .text]

_start:
	mov ax, cs
	mov ds, ax
	mov es, ax

	mov si, _msg
	mov di, _blank
	mov cx, _msg_len
	cld
	rep movsb
```



## 21.标志位入栈、出栈指令（pushf、popf）

​	pushf指令，将标志寄存器的值压栈。popf指令，从栈中弹出数据，送入标志寄存器中。

​	示例：

```
	pushf
	popf
```



# DOSBox应用程序

​	dos环境中有专用的汇编器masm.exe、链接器link.exe，可编译生成直接运行的exe程序。

​	masm与nasm编译的代码风格不同，这里介绍不重新编写代码的方法，生成exe程序。



## 1.生成exe程序

​	新建head.asm，写入下面代码，并在dos下编译：

```
code segment
	db "Hello, world!"
code ends
end
```

​	编译生成head.exe，使用hexdump命令查看内容：

```
	hexdump -C head.exe
```

​	head.exe数据头比直接使用nasn编译的机器码多了512字节，使用dd命令提取这512字节：

```
	dd if=head.exe of=t4_head bs=512 count=1
```

​	将段512字节数据接到nasm编译的机器码前面，即可生成在dox下直接运行的exe程序：

```
	cp t4_head t4.exe
	nasm t4.s
	dd if=t4 of=t4.exe bs=512 count=1 seek=1
```



## 2.在dos终端中显示字符

​	控制dos终端显示的内存空间段地址为0xb800，组合0xb800:0x0f00正好是dos终端倒数第二行开始的位置。

​	地址开始每16bit控制一个字符，低8bit为数据码，高8bit为颜色码。

```
	mov ax, 0xb800
	mov es, ax
	mov di, 0x0f00
	
	mov ah, 3
	mov al, 0x77
	mov [es:di], ax			;终端显示青色的'w'
```



## 3.示例程序4：在终端显示字符串

​	新建t4.s，编写代码：

```
[section .data]

_stack:
	times 128 db 0
_stack_top equ ($)

_msg:
	db "Hello, world!", 0
_msg_len equ ($ - _msg)

[section .text]

_start:
	mov ax, cs
	mov ss, ax
	mov sp, _stack_top

	mov ax, cs
	mov ds, ax
	mov si, _msg

	mov ax, 0xb800
	mov es, ax
	mov di, 0x0f00

	mov cx, _msg_len
	mov ah, 3

_show:
	mov al, [ds:si]
	mov [es:di], ax
	add si, 1
	add di, 2
	loop _show

_exit:
	mov ax, 0x4c00
	int 0x21
```

​	使用“生成exe程序”的方法编译。在dos终端下执行t4.exe，可显示设定好的字符串。

​	程序装载地址在头部数据已初始化，不必使用"org 0x100"伪指令。

​	程序段"_exit"为dos中的程序退出指令。



# 中断



## 1.中断向量表

​	需要CPU立刻去处理的信息：

​	1）除法错误

​	2）单步执行、调试

​	3）执行into指令

​	4）执行int指令

​	中断信息产生后，根据中断类型码，查找中断向量表。中断向量表紧密排列中断程序入口的段地址、偏移地址，表位于0x0000:0x0000 ~ 0x0000:0x03ff 地址处。

​	中断向量表示例：

| 中断类型码 | 说明 | 示例数据 |
| :--: | :--: | :--: |
| 0 | 0号处理中断信息的程序地址 | 0xfff0:0x0000 |
| 1 | 1号处理中断信息的程序地址 | 0xffff:0x0057 |
| 2 | 2号处理中断信息的程序地址 | 0xffff:0x1234 |
| ... | ... | ... |

​	执行int指令，跳转到中断程序后，cs = 类型码 * 4，ip =  类型码 * 4 + 2。



## 2.中断过程（iret）

### 中断过程，CPU自动完成：

​	1）取得中断类型码

​	2）保存标志位寄存器：

```
	pushf
```

​	3）将标志寄存器的TF、IF设置为0

​	4）保存段地址、偏移地址：

```
	push cs
	push ip
```

​	5）查找中断向量表，更新cs:ip：

### 中断处理程序返回的办法：

```
	iret
```

​	上述指令相当于：

```
	pop ip
	pop cs
	popf
```



## 3.中断指令（int、cli、sti）

​	int指令，根据中断类型码调用中断处理程序。示例：

```
	int 0x10
```

​	cli指令，将IF置0，屏蔽掉“可屏蔽中断”。sti指令，将IF置1，允许“可屏蔽中断”。示例：

```
	cli
	sti
```



## 4.示例程序5：编写中断处理程序

